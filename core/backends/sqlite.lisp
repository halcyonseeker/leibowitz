;; A library backend to store the ontology in an SQLite database.

(in-package :leibowitz-core)

(defclass sqlite-library (library)
  ((db-path
    :type (or string pathname)
    :initarg :db-path
    :initform (error "db-path required.")
    :documentation "The path to the SQLite database.")
   (handle
    :type sqlite:sqlite-handle
    :documentation "The handle through which database queries are made."))
  (:documentation "A library backend to store the ontology in a SQLite database."))

(defmethod initialize-instance :after
    ((l sqlite-library) &rest initargs &key &allow-other-keys)
  (declare (ignore initargs))
  (setf (slot-value l 'handle) (sqlite:connect (slot-value l 'db-path)))
  (mapcar (lambda (tbl) (sqlite:execute-non-query (slot-value l 'handle) tbl))
          '("
create table if not exists 'data' (
  'id' text not null unique,
  'type' text not null,
  'birth' datetime not null,
  'modified' datetime not null,
  'terms' text
)" "
create table if not exists 'tags' (
  'name' text not null unique,
  'label' text,
  'count' integer
)" "
create table if not exists 'tag_datum_junctions' (
  'tag_name' text not null,
  'datum_id' text not null,
  unique(tag_name, datum_id) on conflict ignore
)" "
create table if not exists 'tag_predicates' (
  'iftag' text not null,
  'thentag' text not null,
  unique(iftag, thentag) on conflict ignore
)" "
create trigger if not exists inc_tag_count after insert on tag_datum_junctions begin
  update tags set count = count + 1 where name = new.tag_name;
end
" "
create trigger if not exists dec_tag_count after delete on tag_datum_junctions begin
  update tags set count = count - 1 where name = old.tag_name;
  delete from tags where name = old.tag_name and count = 0 and label is null;
end
")))

;;; Reading and writing data

(defmethod add-datum ((l sqlite-library) (d datum))
  (sqlite-nq
   l "insert into data (id, type, birth, modified, terms) values (?, ?, ?, ?, ?)"
   (datum-id d) (datum-kind d) (datum-birth d) (datum-modified d)
   (datum-terms d))
  d)

(defmethod get-datum ((l sqlite-library) id)
  (check-type id (or string pathname))
  (multiple-value-bind
        (id kind birth modified terms)
      (sqlite-row l "select * from data where id = ?" (namestring id))
    (if id
        (make-instance 'datum :id id :kind kind :birth birth
                              :modified modified :terms terms)
        NIL)))

(defmethod del-datum ((l sqlite-library) datum-or-id)
  (check-type datum-or-id (or datum string pathname))
  (let ((id (%need-datum-id datum-or-id)))
    (with-sqlite-tx (l)
      (loop for tag in (get-datum-tags l id)
            do (sqlite-nq l (ccat "delete from tags where name = ? and "
                                  "count = 0 and label is null")
                          (tag-name tag)))
      (sqlite-nq l "delete from data where id = ?" id)
      (sqlite-nq l "delete from tag_datum_junctions where datum_id = ?" id))))

;;; Reading and writing tags

(defmethod add-tag ((l sqlite-library) tag-or-name)
  (check-type tag-or-name (or tag string))
  (when (stringp tag-or-name)
    (setf tag-or-name (make-instance 'tag :name tag-or-name)))
  (sqlite-nq l "insert or ignore into tags (name, label, count) values (?, ?, ?)"
             (tag-name tag-or-name)
             (if (slot-boundp tag-or-name 'label)
                 (tag-label tag-or-name)
                 NIL)
             (tag-count tag-or-name))
  T)

(defmethod get-tag ((l sqlite-library) name)
  (check-type name string)
  (multiple-value-bind
        (name label count)
      (sqlite-row l "select * from tags where name = ?" name)
    (if name
        (make-instance 'tag :name name :label label :count count)
        NIL)))

(defmethod del-tag ((l sqlite-library) tag-or-name)
  (check-type tag-or-name (or datum string))
  (let ((name (%need-tag-name tag-or-name)))
    (with-sqlite-tx (l)
      (sqlite-nq l "delete from tags where name = ?" name)
      (sqlite-nq l "delete from tag_datum_junctions where tag_name = ?" name)
      (sqlite-nq l "delete from tag_predicates where iftag = ? or thentag = ?"
                 name name))))

;;; Reading and writing datum-tag relationships

(defmethod add-datum-tags ((l sqlite-library) datum-or-id tags)
  (check-type datum-or-id (or datum pathname string))
  (check-type tags list)
  (labels ((add-assoc (l name id)
             (add-tag l name)
             (sqlite-nq l (ccat "insert into tag_datum_junctions "
                                "(tag_name, datum_id) values (?, ?)")
                        name id)))
    (with-sqlite-tx (l)
      (loop for tag in tags
            for id = (%need-datum-id datum-or-id)
            for name = (%need-tag-name tag)
            for required-tags = (%cascade-down-predicate-tree l name)
            do (loop for required-tag being each hash-key of required-tags
                     do (add-assoc l required-tag id))))))

(defmethod get-datum-tags ((l sqlite-library) datum-or-id)
  (check-type datum-or-id (or datum pathname string))
  (loop for row in (sqlite-rows l (ccat "select tags.* from tags "
                                        "inner join tag_datum_junctions "
                                        "on tag_name = name where datum_id = ?")
                                (%need-datum-id datum-or-id))
        collect (destructuring-bind (name label count) row
                  (make-instance 'tag :name name :count count :label label))))

(defmethod del-datum-tags ((l sqlite-library) datum-or-id tags &key (cascade NIL))
  (check-type datum-or-id (or datum pathname string))
  (check-type tags list)
  (labels ((del-assoc (l name id)
             (sqlite-nq l (ccat "delete from tag_datum_junctions "
                                "where tag_name = ? and datum_id = ?")
                        name id)
             (sqlite-nq l (ccat "delete from tags where name = ? and "
                                "count = 0 and label is null")
                        name)))
    (with-sqlite-tx (l)
      (loop for tag in tags
            for name = (%need-tag-name tag)
            for id = (%need-datum-id datum-or-id)
            do (del-assoc l name id)
               (when cascade
                 (loop for req being each hash-key of (%cascade-down-predicate-tree l name)
                       do (del-assoc l req id)))))))

(defmethod get-tag-data ((l sqlite-library) tag-or-name)
  (check-type tag-or-name (or string tag))
  (loop for row in (sqlite-rows l (ccat "select data.* from data "
                                        "inner join tag_datum_junctions on "
                                        "datum_id = data.id where tag_name = ?")
                                (%need-tag-name tag-or-name))
        collect (destructuring-bind (id kind birth modified terms) row
                  (make-instance 'datum :id id :kind kind :birth birth
                                        :modified modified :terms terms))))

;;; Reading and writing tag hierarchies

(defmethod add-tag-predicate ((l sqlite-library) iftag-or-name thentag-or-name)
  (check-type iftag-or-name (or tag string))
  (check-type thentag-or-name (or tag string))
  (let ((ifname (%need-tag-name iftag-or-name))
        (thenname (%need-tag-name thentag-or-name)))
    (unless (get-tag l ifname)
      (add-tag l (make-instance 'tag :name ifname)))
    (unless (get-tag l thenname)
      (add-tag l (make-instance 'tag :name thenname)))
    (sqlite-nq l (ccat "insert or ignore into tag_predicates (iftag, thentag)"
                       "values (?, ?)")
               ifname thenname)))

(defmethod get-tag-predicates ((l sqlite-library) tag-or-name)
  (check-type tag-or-name (or tag string))
  (loop for row in (sqlite-rows l (ccat "select tags.* from tags "
                                        "inner join tag_predicates "
                                        "on thentag = name where iftag = ?")
                                (%need-tag-name tag-or-name))
        collect (destructuring-bind (name label count) row
                  (make-instance 'tag :name name :count count :label label))))

(defmethod get-tag-predicands ((l sqlite-library) tag-or-name)
  (check-type tag-or-name (or tag string))
  (loop for row in (sqlite-rows l (ccat "select tags.* from tags "
                                        "inner join tag_predicates "
                                        "on iftag = name where thentag = ?")
                                (%need-tag-name tag-or-name))
        collect (destructuring-bind (name label count) row
                  (make-instance 'tag :name name :count count :label label))))

(defmethod del-tag-predicate ((l sqlite-library) iftag-or-name thentag-or-name)
  (check-type iftag-or-name (or tag string))
  (check-type thentag-or-name (or tag string))
  (let ((ifname (%need-tag-name iftag-or-name))
        (thenname (%need-tag-name thentag-or-name)))
    (sqlite-nq l "delete from tag_predicates where iftag = ? and thentag = ?"
               ifname thenname)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Additional Methods

(defmethod sqlite-nq ((l sqlite-library) &rest args)
  "Run a non-query on this database."
  (apply #'sqlite:execute-non-query (nconc (list (slot-value l 'handle)) args)))

(defmethod sqlite-row ((l sqlite-library) &rest args)
  "Run a query on this database that returns a single row."
  (apply #'sqlite:execute-one-row-m-v (nconc (list (slot-value l 'handle)) args)))

(defmethod sqlite-rows ((l sqlite-library) &rest args)
  "Run a query on this database that returns multiple rows."
  (apply #'sqlite:execute-to-list (nconc (list (slot-value l 'handle)) args)))

(defmacro with-sqlite-tx ((sqlite-library) &body body)
  "Run BODY as an atomic SQLite transaction."
  `(progn
     (sqlite-nq ,sqlite-library "begin transaction")
     (handler-case (progn ,@body)
       (T (c)
         (sqlite-nq ,sqlite-library "rollback")
         (error c))
       (:no-error (c)
         (declare (ignore c))
         (sqlite-nq ,sqlite-library "commit")))))

(defmacro ccat (&rest strings)
  "Concatenate some strings at compile-time.  Used internally to shorten
lines with really long SQL queries."
  (format NIL "窿篝蜷铉螬ㄤ彐躅ャ狍汜溴滹黝痱邃殂狒瀛趄邋扉蝻雉镳糸镱犰翕紊泰⑶轹孱蚁显狍翳蝻雉翎镦翎栝弪狎汨趄狯弪箦滹黝轸犷蝈趱蝾栳箬翎忪镦犰翎珞翳狒箬秕熹忮徜溴洚涉麇孱泔躅翦翎翳狒轶犰蝈徜轭翳翎忪镦翎珞麸徜洮箝眇禊箅轲轸殄泫沆弩轭翳翎栝弪狎汨狎牾篝殓铒蝈洚换抿遽翦翳栳箬翎忪翳骈蝮糸礤翳轶骢钽糸镱轶汜祆邃躅戾篌翕箦翩翕磲脲栳箬翎忪呼弩＇羼踽飑┅换娘蝈沲蝮轹怛遽漪璀骈蝮箦狎汨镦翳珧狃璎箅轲痖铉换麒孱弼弪麇骈钿翎翳狒麽痱弼轱躞禊孱泔躅翦蝈轭矧溴换麸狯镩蝈沲蝮轭轭骈铋翦禊箦翩ㄧ弭栳箬蝻雉翕飑蝻雉祜镳骘翎轭ㄧ弭翎绛痱邃殂狒弩扉蝻雉骘钺礤翎绛钺礤翎绌躅戾篌ㄧ弭栳箬翎绛钺礤翎绌翕飑滹箦翩ㄧ弭栳箬翎绛钺礤翎绌翕飑翎绛钺礤翎绌ē汜筱徜瀛滹黝痱邃殂狒瀛趄邋扉翎绛钺礤翎绌翕飑翕飑